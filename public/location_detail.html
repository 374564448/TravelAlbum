<!DOCTYPE html>
<html lang="zh-CN">
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, viewport-fit=cover">
  <title>Travel Album - 照片明细</title>
  <link rel="stylesheet" href="css/location_detail.css">
</head>
<body>

  <!-- 樱花飘落背景 -->
  <canvas id="sakura"></canvas>

  <!-- 滚动进度条 -->
  <div class="scroll-progress" id="scrollProgress"></div>

  <!-- 顶部标题栏 -->
  <header class="detail-header" id="detailHeader">
    <a class="back-btn" id="backBtn" href="javascript:void(0)">&#8592; 返回</a>
    <h1 class="detail-title" id="detailTitle"></h1>
  </header>

  <!-- 照片列表 -->
  <div class="photo-grid" id="photoGrid"></div>

  <!-- 页面过渡遮罩（进入时渐隐） -->
  <div class="page-overlay active" id="pageOverlay"></div>
  <script>
    // 立即根据主题设置遮罩颜色，避免进入/离开时出现黑白闪跳
    (function () {
      var KEY = 'travelAlbum_theme';
      var theme = localStorage.getItem(KEY) || 'auto';
      if (theme === 'auto') {
        var h = new Date().getHours();
        theme = (h >= 7 && h < 18) ? 'day' : 'night';
      }
      if (theme === 'day') {
        document.getElementById('pageOverlay').style.background = '#E8F4F8';
      }
    })();
  </script>

  <!-- 照片放大查看层 -->
  <div class="lightbox" id="lightbox">
    <div class="lightbox-counter" id="lightboxCounter"></div>
    <div class="lightbox-frame" id="lightboxFrame">
      <div class="lightbox-frame-img" id="lightboxFrameImg">
        <img class="lightbox-img" id="lightboxImg" src="" alt="">
      </div>
      <div class="lightbox-frame-footer" id="lightboxFooter">
        <div class="lightbox-frame-title" id="lightboxTitle"></div>
        <div class="lightbox-frame-desc" id="lightboxDesc"></div>
      </div>
    </div>

    <!-- 移动端首次滑动提示 -->
    <div class="swipe-hint" id="swipeHint">
      <span class="swipe-hint-arrow swipe-hint-left">&#8249;</span>
      <span class="swipe-hint-text">左右滑动切换照片</span>
      <span class="swipe-hint-arrow swipe-hint-right">&#8250;</span>
    </div>
  </div>

  <!-- ===== WebGL Shader 脚本（樱花渲染必需，必须内联） ===== -->
  <!-- sakura vertex shader -->
  <script id="sakura_point_vsh" type="x-shader/x_vertex">
uniform mat4 uProjection;
uniform mat4 uModelview;
uniform vec3 uResolution;
uniform vec3 uOffset;
uniform vec3 uDOF;
uniform vec3 uFade;
attribute vec3 aPosition;
attribute vec3 aEuler;
attribute vec2 aMisc;
varying vec3 pposition;
varying float psize;
varying float palpha;
varying float pdist;
varying vec3 normX;
varying vec3 normY;
varying vec3 normZ;
varying vec3 normal;
varying float diffuse;
varying float specular;
varying float rstop;
varying float distancefade;
void main(void) {
    vec4 pos = uModelview * vec4(aPosition + uOffset, 1.0);
    gl_Position = uProjection * pos;
    gl_PointSize = aMisc.x * uProjection[1][1] / -pos.z * uResolution.y * 0.5;
    pposition = pos.xyz;
    psize = aMisc.x;
    pdist = length(pos.xyz);
    palpha = smoothstep(0.0, 1.0, (pdist - 0.1) / uFade.z);
    vec3 elrsn = sin(aEuler);
    vec3 elrcs = cos(aEuler);
    mat3 rotx = mat3(1.0, 0.0, 0.0, 0.0, elrcs.x, elrsn.x, 0.0, -elrsn.x, elrcs.x);
    mat3 roty = mat3(elrcs.y, 0.0, -elrsn.y, 0.0, 1.0, 0.0, elrsn.y, 0.0, elrcs.y);
    mat3 rotz = mat3(elrcs.z, elrsn.z, 0.0, -elrsn.z, elrcs.z, 0.0, 0.0, 0.0, 1.0);
    mat3 rotmat = rotx * roty * rotz;
    normal = rotmat[2];
    mat3 trrotm = mat3(
        rotmat[0][0], rotmat[1][0], rotmat[2][0],
        rotmat[0][1], rotmat[1][1], rotmat[2][1],
        rotmat[0][2], rotmat[1][2], rotmat[2][2]
    );
    normX = trrotm[0]; normY = trrotm[1]; normZ = trrotm[2];
    const vec3 lit = vec3(0.6917144638660746, 0.6917144638660746, -0.20751433915982237);
    float tmpdfs = dot(lit, normal);
    if(tmpdfs < 0.0) { normal = -normal; tmpdfs = dot(lit, normal); }
    diffuse = 0.4 + tmpdfs;
    vec3 eyev = normalize(-pos.xyz);
    if(dot(eyev, normal) > 0.0) {
        vec3 hv = normalize(eyev + lit);
        specular = pow(max(dot(hv, normal), 0.0), 20.0);
    } else { specular = 0.0; }
    rstop = clamp((abs(pdist - uDOF.x) - uDOF.y) / uDOF.z, 0.0, 1.0);
    rstop = pow(rstop, 0.5);
    distancefade = min(1.0, exp((uFade.x - pdist) * 0.69315 / uFade.y));
}
  </script>

  <!-- sakura fragment shader -->
  <script id="sakura_point_fsh" type="x-shader/x_fragment">
#ifdef GL_ES
precision highp float;
#endif
uniform vec3 uDOF;
uniform vec3 uFade;
const vec3 fadeCol = vec3(0.08, 0.03, 0.06);
varying vec3 pposition;
varying float psize;
varying float palpha;
varying float pdist;
varying vec3 normX;
varying vec3 normY;
varying vec3 normZ;
varying vec3 normal;
varying float diffuse;
varying float specular;
varying float rstop;
varying float distancefade;
float ellipse(vec2 p, vec2 o, vec2 r) {
    vec2 lp = (p - o) / r;
    return length(lp) - 1.0;
}
void main(void) {
    vec3 p = vec3(gl_PointCoord - vec2(0.5, 0.5), 0.0) * 2.0;
    vec3 d = vec3(0.0, 0.0, -1.0);
    float nd = normZ.z;
    if(abs(nd) < 0.0001) discard;
    float np = dot(normZ, p);
    vec3 tp = p + d * np / nd;
    vec2 coord = vec2(dot(normX, tp), dot(normY, tp));
    const float flwrsn = 0.258819045102521;
    const float flwrcs = 0.965925826289068;
    mat2 flwrm = mat2(flwrcs, -flwrsn, flwrsn, flwrcs);
    vec2 flwrp = vec2(abs(coord.x), coord.y) * flwrm;
    float r;
    if(flwrp.x < 0.0) { r = ellipse(flwrp, vec2(0.065, 0.024) * 0.5, vec2(0.36, 0.96) * 0.5); }
    else { r = ellipse(flwrp, vec2(0.065, 0.024) * 0.5, vec2(0.58, 0.96) * 0.5); }
    if(r > rstop) discard;
    vec3 col = mix(vec3(1.0, 0.8, 0.75), vec3(1.0, 0.9, 0.87), r);
    float grady = mix(0.0, 1.0, pow(coord.y * 0.5 + 0.5, 0.35));
    col *= vec3(1.0, grady, grady);
    col *= mix(0.8, 1.0, pow(abs(coord.x), 0.3));
    col = col * diffuse + specular;
    col = mix(fadeCol, col, distancefade);
    float alpha = (rstop > 0.001)? (0.5 - r / (rstop * 2.0)) : 1.0;
    alpha = smoothstep(0.0, 1.0, alpha) * palpha;
    gl_FragColor = vec4(col * 0.5, alpha);
}
  </script>

  <!-- effects common vertex shader -->
  <script id="fx_common_vsh" type="x-shader/x_vertex">
uniform vec3 uResolution;
attribute vec2 aPosition;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    gl_Position = vec4(aPosition, 0.0, 1.0);
    texCoord = aPosition.xy * 0.5 + vec2(0.5, 0.5);
    screenCoord = aPosition.xy * vec2(uResolution.z, 1.0);
}
  </script>

  <!-- background fragment shader -->
  <script id="bg_fsh" type="x-shader/x_fragment">
#ifdef GL_ES
precision highp float;
#endif
uniform vec2 uTimes;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    vec3 col;
    float c;
    vec2 tmpv = texCoord * vec2(0.8, 1.0) - vec2(0.95, 1.0);
    c = exp(-pow(length(tmpv) * 1.8, 2.0));
    col = mix(vec3(0.02, 0.0, 0.03), vec3(0.96, 0.98, 1.0) * 1.5, c);
    gl_FragColor = vec4(col * 0.5, 1.0);
}
  </script>

  <!-- bright buffer fragment shader -->
  <script id="fx_brightbuf_fsh" type="x-shader/x_fragment">
#ifdef GL_ES
precision highp float;
#endif
uniform sampler2D uSrc;
uniform vec2 uDelta;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    vec4 col = texture2D(uSrc, texCoord);
    gl_FragColor = vec4(col.rgb * 2.0 - vec3(0.5), 1.0);
}
  </script>

  <!-- direction blur fragment shader -->
  <script id="fx_dirblur_r4_fsh" type="x-shader/x_fragment">
#ifdef GL_ES
precision highp float;
#endif
uniform sampler2D uSrc;
uniform vec2 uDelta;
uniform vec4 uBlurDir;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    vec4 col = texture2D(uSrc, texCoord);
    col = col + texture2D(uSrc, texCoord + uBlurDir.xy * uDelta);
    col = col + texture2D(uSrc, texCoord - uBlurDir.xy * uDelta);
    col = col + texture2D(uSrc, texCoord + (uBlurDir.xy + uBlurDir.zw) * uDelta);
    col = col + texture2D(uSrc, texCoord - (uBlurDir.xy + uBlurDir.zw) * uDelta);
    gl_FragColor = col / 5.0;
}
  </script>

  <!-- common effect fragment shader -->
  <script id="fx_common_fsh" type="x-shader/x_fragment">
#ifdef GL_ES
precision highp float;
#endif
uniform sampler2D uSrc;
uniform vec2 uDelta;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    gl_FragColor = texture2D(uSrc, texCoord);
}
  </script>

  <!-- post process vertex shader -->
  <script id="pp_final_vsh" type="x-shader/x_vertex">
uniform vec3 uResolution;
attribute vec2 aPosition;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    gl_Position = vec4(aPosition, 0.0, 1.0);
    texCoord = aPosition.xy * 0.5 + vec2(0.5, 0.5);
    screenCoord = aPosition.xy * vec2(uResolution.z, 1.0);
}
  </script>

  <!-- post process fragment shader -->
  <script id="pp_final_fsh" type="x-shader/x_fragment">
#ifdef GL_ES
precision highp float;
#endif
uniform sampler2D uSrc;
uniform sampler2D uBloom;
uniform vec2 uDelta;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
    vec4 srccol = texture2D(uSrc, texCoord) * 2.0;
    vec4 bloomcol = texture2D(uBloom, texCoord);
    vec4 col;
    col = srccol + bloomcol * (vec4(1.0) + srccol);
    col *= smoothstep(1.0, 0.0, pow(length((texCoord - vec2(0.5)) * 2.0), 1.2) * 0.5);
    col = pow(col, vec4(0.45454545454545));
    gl_FragColor = vec4(col.rgb, 1.0);
    gl_FragColor.a = 1.0;
}
  </script>

  <!-- 竖屏提示 -->
  <div class="portrait-overlay">
    <div class="portrait-icon">
      <svg viewBox="0 0 100 100" fill="none" xmlns="http://www.w3.org/2000/svg">
        <rect x="25" y="10" width="50" height="80" rx="8" stroke="rgba(0,195,255,0.8)" stroke-width="3"/>
        <circle cx="50" cy="78" r="4" fill="rgba(0,195,255,0.6)"/>
      </svg>
    </div>
    <p class="portrait-text">请横屏观看</p>
  </div>

  <script src="js/sakura.js"></script>
  <script src="js/location_detail.js"></script>
</body>
</html>
